# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>
# Contributor: Ariel Popper <a@arielp.com>

_gitname=powerline
_gitbranch=develop
pkgname="otf-powerline-symbols-git"
pkgver=827.70e279a
pkgrel=1
pkgdesc='Fallback font for the Powerline symbols'
url='https://github.com/Lokaltog/powerline'
license=('MIT')
arch=('any')
makedepends=('git')
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}" "PowerlineSymbols.otf")
install=powerline-symbols.install
sha256sums=('SKIP' 'ff5a78acdc2d189f2293511ded269427c88d8f4d3b19a7cfab60342cf522a385')

pkgver() {
	cd "${srcdir}/${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

prepare() {
	mv "${srcdir}/PowerlineSymbols.otf" "${srcdir}/${_gitname}/font/PowerlineSymbols.otf"
}

package() {
	cd "${srcdir}/${_gitname}"

	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"
	
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
