# Maintainer: Evan Purkhiser <evanpurkhiser@gmail.com>
# Contributor: Jack Mitchell <jack@embed.me.uk>

pkgname=xboxdrv
pkgver=0.8.5
pkgrel=2
url="http://pingus.seul.org/~grumbel/xboxdrv/"
pkgdesc="An XBox/XBox 360 gamepad driver - as alternative to the xpad-kernel module - with more configurability, runs in userspace and supports a multitude of controllers"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('libx11' 'dbus-glib' 'libusbx')
makedepends=('scons' 'boost' 'pkg-config' 'libx11' 'dbus-glib' 'libusb')

source=(
  "http://pingus.seul.org/~grumbel/xboxdrv/$pkgname-linux-$pkgver.tar.bz2"
  'xboxdrv.service'
  'xboxdrv.conf'
  '0001-Buttonevent-exec-Double-fork-to-avoid-zombies.patch'
)

md5sums=('7f20b12361770bbff9414a7c6d522c25'
         'c44dc13f6d34cd7fc61c87ecd8c3a547'
         'c73bb9cf8ff763e7c477366472d19813'
         '2d3b80e2efc834a676b0b03f798efcf8')

prepare() {

  cd "$srcdir/$pkgname-linux-$pkgver"
  patch -p1 -i "$srcdir/0001-Buttonevent-exec-Double-fork-to-avoid-zombies.patch"

}

build() {

  cd "$srcdir/$pkgname-linux-$pkgver"
  scons $MAKEFLAGS 

}

package() {

  cd "$srcdir/$pkgname-linux-$pkgver"
  make install PREFIX=/usr DESTDIR="$pkgdir"

  install -D -m644 "$srcdir/xboxdrv.service" "$pkgdir/usr/lib/systemd/system/xboxdrv.service"
  install -D -m644 "$srcdir/xboxdrv.conf" "$pkgdir/etc/conf.d/xboxdrv"

} 
